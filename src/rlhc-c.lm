void c_tok_list( TL: host::tok* )
{
	for Tok: host::tok in repeat(TL) {
		if match Tok [host::`${ StmtList: stmt* host::`}$]
			c_stmt_list( StmtList )
		elsif match Tok [host::`={ Expr: expr host::`}=]
			c_expr( Expr )
		else {
			send Output [Tok]
		}
	}
}

void c_expr_factor( ExprFactor: expr_factor )
{
	if match ExprFactor
		['$' string uint '{' TL: host::tok* host::`}$]
	{
		c_tok_list( TL )
	}
	elsif match ExprFactor
		['=' string uint '{' TL: host::tok* host::`}$]
	{
		c_tok_list( TL )
	}
	elsif match ExprFactor
		[O:`( TL: expr C: `)]
	{
		send Output
			[O c_expr(TL) C]
	}
	elsif match ExprFactor
		[ident O: `[ TL: expr C: `]]
	{
		send Output
			[ExprFactor.ident O c_expr( TL ) C]
	}
	elsif match ExprFactor
		['offset' '(' ident ',' expr ')']
	{
		send Output
			[ExprFactor.ident ' + ' c_expr( ExprFactor.expr )]
	}
	elsif match ExprFactor
		['deref' '(' ident ',' expr ')']
	{
		send Output
			"(*( [c_expr(ExprFactor.expr)] ))
	}
	elsif match ExprFactor
			[T: `TRUE]
	{
		T.data = '1'
		send Output [T]
	}
	elsif match ExprFactor
			[F: `FALSE]
	{
		F.data = '0'
		send Output [F]
	}
	elsif match ExprFactor
			[N: `nil]
	{
		N.data = '0'
		send Output [N]
	}
	elsif match ExprFactor
			[Number: number]
	{
		c_number( Number )
	}
	else {
		# Catches cases not specified
		send Output [ExprFactor]
	}
}

void c_expr_ptr( ExprPtr: expr_ptr )
{
	if match ExprPtr [expr_ptr Arrow: `-> expr_factor]
	{
		c_expr_ptr( ExprPtr._expr_ptr )
		send Output [Arrow]
		c_expr_factor( ExprPtr.expr_factor )
	}
	elsif match ExprPtr [Star: `* expr_ptr]
	{
		send Output [Star]
		c_expr_ptr( ExprPtr._expr_ptr )
	}
	elsif match ExprPtr [PP: `++ expr_ptr]
	{
		send Output [PP]
		c_expr_ptr( ExprPtr._expr_ptr )
	}
	elsif match ExprPtr [MM: `-- expr_ptr]
	{
		send Output [MM]
		c_expr_ptr( ExprPtr._expr_ptr )
	}
	elsif match ExprPtr [expr_ptr PP: `++]
	{
		c_expr_ptr( ExprPtr._expr_ptr )
		send Output [PP]
	}
	elsif match ExprPtr [expr_ptr MM: `--]
	{
		c_expr_ptr( ExprPtr._expr_ptr )
		send Output [MM]
	}
	elsif match ExprPtr [Open: `( Type: type Close: `) expr_ptr]
	{
		send Output [Open]
		c_type( Type )
		send Output [Close]
		c_expr_ptr( ExprPtr._expr_ptr )
	}
	elsif match ExprPtr [expr_factor]
	{
		c_expr_factor( ExprPtr.expr_factor )
	}
	else {
		send Output [ExprPtr]
	}
}

void c_expr_factor_op( ExprFactorOp: expr_factor_op )
{
	if match ExprFactorOp [B: `! expr_factor_op]
	{
		send Output [B]
		c_expr_factor_op( ExprFactorOp._expr_factor_op )
	}
	elsif match ExprFactorOp [T: `~ expr_factor_op]
	{
		send Output [T]
		c_expr_factor_op( ExprFactorOp._expr_factor_op )
	}
	elsif match ExprFactorOp [expr_ptr]
	{
		c_expr_ptr( ExprFactorOp.expr_ptr )
	}
}

void c_expr_bitwise( ExprBitwise: expr_bitwise )
{
	if match ExprBitwise [expr_bitwise A: `& expr_factor_op]
	{
		c_expr_bitwise( ExprBitwise._expr_bitwise )
		send Output [A]
		c_expr_factor_op( ExprBitwise.expr_factor_op )
	}
	elsif match ExprBitwise [expr_factor_op]
	{
		c_expr_factor_op( ExprBitwise.expr_factor_op )
	}
}

void c_expr_mult( ExprMult: expr_mult )
{
	if match ExprMult [expr_mult T: `* expr_bitwise]
	{
		c_expr_mult( ExprMult._expr_mult )
		send Output [T]
		c_expr_bitwise( ExprMult.expr_bitwise )
	}
	elsif match ExprMult [expr_bitwise]
	{
		c_expr_bitwise( ExprMult.expr_bitwise )
	}
}

void c_expr_add( ExprAdd: expr_add )
{
	if match ExprAdd [expr_add Op: add_op expr_mult]
	{
		c_expr_add( ExprAdd._expr_add )
		send Output [Op]
		c_expr_mult( ExprAdd.expr_mult )
	}
	elsif match ExprAdd [expr_mult]
	{
		c_expr_mult( ExprAdd.expr_mult )
	}
}

void c_expr_shift( ExprShift: expr_shift )
{
	if match ExprShift [expr_shift Op: shift_op expr_add]
	{
		c_expr_shift( ExprShift._expr_shift )
		send Output [Op]
		c_expr_add( ExprShift.expr_add )
	}
	elsif match ExprShift [expr_add]
	{
		c_expr_add( ExprShift.expr_add )
	}
}

void c_expr_test( ExprTest: expr_test )
{
	if match ExprTest [expr_test Op: test_op expr_shift]
	{
		c_expr_test( ExprTest._expr_test )
		send Output [Op]
		c_expr_shift( ExprTest.expr_shift )
	}
	elsif match ExprTest [expr_shift]
	{
		c_expr_shift( ExprTest.expr_shift )
	}
}

void c_expr_cond( ExprCond: expr_cond )
{
	c_expr_test( ExprCond.expr_test )
}

void c_expr_assign( ExprAssign: expr_assign )
{
	if match ExprAssign [expr_assign Op: assign_op expr_cond]
	{
		c_expr_assign( ExprAssign._expr_assign )
		send Output [Op]
		c_expr_cond( ExprAssign.expr_cond )
	}
	elsif match ExprAssign [expr_cond]
	{
		c_expr_cond( ExprAssign.expr_cond )
	}
}

void c_expr( Expr: expr )
{
	c_expr_assign( Expr.expr_assign )
}

void c_type( Type: type )
{
	if match Type "s8"
		send Output ['char ']
	elsif match Type "s16"
		send Output ['short ']
	elsif match Type "s32"
		send Output ['int ']
	elsif match Type "s64"
		send Output ['long ']
	elsif match Type "s128"
		send Output ['long long ']
	elsif match Type "uint"
		send Output ['unsigned int ']
	else
		send Output [Type]
}

void c_number( Number: number )
{
	if match Number [`u `( uint `) ]
		send Output "[Number.uint]u"
	else
		send Output [Number]
}

void c_num_list( NumList: num_list )
{
	for Number: number in NumList
		send Output "[c_number( Number )], "
}

void c_stmt( Stmt: stmt )
{
	if match Stmt
		['$' string uint '{' TL: host::tok* host::`}$]
	{
		c_tok_list( TL )
	}
	elsif match Stmt
		['=' string uint '{' TL: host::tok* host::`}$]
	{
		c_tok_list( TL )
	}
	elsif match Stmt [Array: array] {
		send Output
			"static const [c_type(Array.type)] "
				"[Array.ident] \[\] = { [c_num_list(Array.num_list)] };
	}
	elsif match Stmt [Value: value] {
		send Output
			"static const [Value.value_pass]
	}
	elsif match Stmt [
			'if' O: `( IfExpr: expr C: `) IfStmt: stmt
			ElseIfClauseList: else_if_clause* ElseClauseOpt: else_clause?
	] {
		send Output
			"if ( [c_expr(IfExpr)] )
			"	[c_stmt(IfStmt)]

		for ElseIfClause: else_if_clause in repeat( ElseIfClauseList ) {
			match ElseIfClause
				['else if (' ElseIfExpr: expr ')' ElseIfStmt: stmt]

			send Output
				"else if ( [c_expr(ElseIfExpr)] )
				"	[c_stmt(ElseIfStmt)]
		}

		if ( match ElseClauseOpt ['else' ElseStmt: stmt] ) {
			send Output
				"else
				"	[c_stmt(ElseStmt)]
		}
	}
	elsif match Stmt ['while' '(' WhileExpr: expr ')' WhileStmt: stmt] {
		send Output
			"while ( [c_expr(WhileExpr)] )
			"	[c_stmt(WhileStmt)]
	}
	elsif match Stmt ['switch' '(' SwitchExpr: expr ')' '{' StmtList: stmt* '}'] {
		send Output
			"switch ( [c_expr(SwitchExpr)] ) {
			"	[c_stmt_list(StmtList)]
			"}
	}
	elsif match Stmt [ExprExpr: expr Semi: `;] {
		send Output
			[c_expr(ExprExpr) Semi]
	}
	elsif match Stmt [L: `{ TL: stmt* R: `}] {
		send Output
			[L c_stmt_list(TL) R]
	}
	elsif match Stmt [TypeList: opt_const Type: type OptPtr: opt_ptr Ident: ident OptInit: opt_init Semi: `;]
	{
		send Output
			[TypeList c_type(Type) OptPtr Ident]

		if match OptInit [E: `= expr] {
			send Output
				[E c_expr(OptInit.expr)]
		}

		send Output
			[Semi]
	}
	elsif match Stmt [Export: export_stmt]
	{
		send Output
			"#define [Export.ident] [c_number(Export.number)] 
	}
	elsif match Stmt ['fallthrough' ';']
	{
		# Nothing needed here.
	}
	elsif match Stmt [Index: index_stmt]
	{
		send Output
			"const [c_type(Index.type)] *[Index.ident];
	}
	elsif match Stmt [case_label]
	{
		send Output
			"case [c_expr( Stmt.case_label.expr )]:
	}
	else {
		# catches unspecified cases
		send Output [Stmt]
	}
}

void c_stmt_list( StmtList: stmt* )
{
	for Stmt: stmt in repeat( StmtList )
		c_stmt( Stmt )
}
